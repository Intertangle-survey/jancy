//..............................................................................
//
// Mailslot Monitor log packet declarations and representer
//

import "log.jnc"

//..............................................................................

enum MailslotMonLogRecordCode
{
	CaptureStarted, // don't mix with MailslotLogRecordCode
	CaptureStopped,
	CaptureError,
	ServerFileOpened,
	ServerFileOpenError,
	ClientFileOpened,
	ClientFileOpenError,
	FileClosed,
	FileIdChanged,
}

enum MailslotMonLogPartCode
{
	Prefix = log.PartCodeFlags.MergeableForward | 1,
	Suffix = log.PartCodeFlags.MergeableBackward | 1,
}

//..............................................................................

bool representMailslotMonLog  (
	log.RepresenterTarget* target,
	uint_t recordCode,
	void const* p,
	size_t size,
	uint_t foldFlags
	)
{
	switch (recordCode)
	{
	case MailslotMonLogRecordCode.CaptureStarted:
		char const* nameFilter = (char const*) p;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Listen;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText ($"Capture started with filter \e[34m$nameFilter");
		break;

	case MailslotMonLogRecordCode.CaptureStopped:
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.StopListen;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText ("Capture stopped");
		break;

	case MailslotMonLogRecordCode.CaptureError:
		std.Error const* error = (std.Error const*) p;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		target.m_lineAttr.m_backColor = log.StdLogColor.Error;
		target.addHyperText ($"Cannot start capture: $(error.m_description)");
		break;

	case MailslotMonLogRecordCode.ServerFileOpened:
		uint32_t fileId = *(uint32_t const*) p;
		char const* name = (char const*) (p + sizeof (fileId));
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Connect;
		target.m_lineAttr.m_backColor = log.StdLogColor.Connect;
		target.addHyperText ($"File \e[34m#$fileId\e[m: Server file opened: \e[34m$name");
		break;

	case MailslotMonLogRecordCode.ServerFileOpenError:
		std.Error const* error = (std.Error const*) p;
		char const* name = (char const*) (error + 1);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		target.m_lineAttr.m_backColor = log.StdLogColor.Error;
		target.addHyperText ($"Cannot open server file \e[34m$name\e[m$: $(error.m_description)");
		break;

	case MailslotMonLogRecordCode.ClientFileOpened:
		uint32_t fileId = *(uint32_t const*) p;
		char const* name = (char const*) (p + sizeof (fileId));
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Connect;
		target.m_lineAttr.m_backColor = log.StdLogColor.Connect;
		target.addHyperText ($"File \e[34m#$fileId\e[m: Client file opened: \e[34m$name");
		break;

	case MailslotMonLogRecordCode.ClientFileOpenError:
		std.Error const* error = (std.Error const*) p;
		char const* name = (char const*) (error + 1);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		target.m_lineAttr.m_backColor = log.StdLogColor.Error;
		target.addHyperText ($"Cannot open client file \e[34m$name\e[m: $(error.m_description)");
		break;

	case MailslotMonLogRecordCode.FileClosed:
		target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Disconnect;
		target.m_lineAttr.m_backColor = log.StdLogColor.Disconnect;
		target.addHyperText (MailslotMonLogPartCode.Suffix, "File closed");
		break;

	case MailslotMonLogRecordCode.FileIdChanged:
		uint32_t fileId = *(uint32_t const*) p;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (MailslotMonLogPartCode.Prefix, $"File \e[34m#$fileId: ");
		break;

	default:
		return false;
	}

	return true;
}

//..............................................................................
